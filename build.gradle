import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'io.papermc.paperweight.userdev' version '1.7.7'
    id 'xyz.jpenilla.run-paper' version '2.2.2'
    id 'eclipse'
    id 'java'
}

def supportedJava = 17

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(supportedJava))
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-source', '' + supportedJava, '-target', '' + supportedJava]
}

compileTestJava.options.encoding = 'UTF-8'
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()

    // Spigot
    maven {
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    // Bungeecord
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }

    // Paper and Adventure API
    maven {
        url 'https://repo.papermc.io/repository/maven-public/'
    }

    // ProtocolLib
    maven {
        url "https://repo.dmulloy2.net/repository/public/"
    }

    // Skript
    maven {
        url 'https://repo.skriptlang.org/releases'
    }

}

dependencies {

    //Spigot/Bukkit
    implementation (group: 'org.spigotmc', name: 'spigot-api', version: '1.21.4-R0.1-SNAPSHOT')
    paperweight.paperDevBundle("1.20.4-R0.1-SNAPSHOT") // Used for reverse engineering

    // Nullable annotation
    implementation (group: 'org.eclipse.jdt', name: 'org.eclipse.jdt.annotation', version: '2.3.100')

    // ProtocolLib
    implementation (group: "com.comphenix.protocol", name: "ProtocolLib", version: "5.1.0") {
        transitive = false
    }

    // Skript
    implementation (group: 'com.github.SkriptLang', name: 'Skript', version: '2.10.1') {
        transitive = false
    }

    // Adventure API serializer to text components for Bukkit.
    shadow group: 'net.kyori', name: 'adventure-text-serializer-bungeecord', version: '4.3.4'

    shadow group: 'org.bstats', name: 'bstats-bukkit', version: '3.1.0'

}

processResources {
    filter ReplaceTokens, tokens: ["version": project.property("version")]
       from ('lang') {
        include '**/*'
        into 'lang/'
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    minimize()
}
